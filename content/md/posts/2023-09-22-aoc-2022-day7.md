{
:title "Experimenting with Clojure: Advent of Code 2022 - Day 7"
:toc true
:tags ["Clojure"]
:description "I've been getting some Clojure practice in by tackling the Advent of Code puzzles. 2022's Day 7 presented a scenario that sounded like a good fit for some Clojure namespaces I'd read about, but never used before. I was pleasantly surprised with the results."
}

It should go without saying, but **spoilers for Advent of Code 2022 Day 7**. And while I'm spoiling things up front, the namespaces I used to tackle the problem are `clojure.zip` and `clojure.walk`. Read on to see how![^1]

The [puzzle](https://adventofcode.com/2022/day/7) I'll be covering in this article is as follows:

Given some terminal output that show the results of navigating a filesystem, find all the directories with a total size of at most 100 000 and sum the sizes of those directories. The given sample terminal output (puzzle input) is:

```
$ cd /
$ ls
dir a
14848514 b.txt
8504156 c.dat
dir d
$ cd a
$ ls
dir e
29116 f
2557 g
62596 h.lst
$ cd e
$ ls
584 i
$ cd ..
$ cd ..
$ cd d
$ ls
4060174 j
8033020 d.log
5626152 d.ext
7214296 k
```

There's probably some clever way to handle this, but my first thought was to break the problem up into very simple steps and not worry too much about algorithmic efficiency. I figured in order to solve this puzzle, I would have to:

1. Parse the given input into a filesystem tree
2. Calculate the size of each directory in the tree
3. Walk the tree and filter the directory nodes with size of at most 100 000
4. Sum the sizes of the filtered directory nodes

## Constructing the Filesystem

It seemed to me that step 1 would require some interesting recursion with backtracking, where I would have to keep track of the tree so far so that `cd ..` would take me to the right place. I didn't find the thought of doing that particularly exciting, but then I remembered an interesting discovery I made a while ago while browsing Clojure API docs: a namespace called `clojure.zip`.

This documentation for this little namespace is even more terse than some of the tersest stuff in `clojure.core`, but it was intriguing enough to me at the time that I'd read some articles and watched some videos about it; I knew from prior exploration that this namespace provides a "functional zipper" that is really good at moving around a tree-like structure (e.g. `up`, `down`) and making changes to that structure (e.g. `append-child`). That seemed right for the problem of parsing the given input into a filesystem tree.

Here's how it looks (note that in the following code, `clojure.zip` is aliased as `z`):

```clojure
(defn dir [name]
  {:name name :children []})

(defn file [name size]
  {:name name :size (parse-long size)})

(defn dir? [file]
  (contains? file :children))

(defn fs-zipper [root]
  (z/zipper dir? :children (fn [n coll] (assoc n :children coll)) root))

(defn insert-dir [loc [_ name]]
  (z/append-child loc (dir name)))

(defn insert-file [loc [_ size name]]
  (z/append-child loc (file name size)))

(defn dir-loc [name loc]
  (let [file (z/node loc)]
    (when (and (dir? file) (= (:name file) name))
      loc)))

(defn nav-to-dir [loc [_ name]]
  (some (partial dir-loc name) (->> loc z/down (iterate z/right))))

(defn parse
  [loc input]
  (condp re-find input
    #"\$ cd /" (fs-zipper (dir "/"))
    #"\$ ls" loc
    #"dir (.+)" :>> (partial insert-dir loc)
    #"(\d+) (.+)" :>> (partial insert-file loc)
    #"cd \.\." (z/up loc)
    #"cd (.+)" :>> (partial nav-to-dir loc)))
```

The workhorse of the above code is `parse`, and it's intended to be used as a reducing function to build up the filesystem tree using a zipper (the terminology used by the zipper functions to indicate the current location you're pointing to in the tree is "loc"). Once the tree has been build up with the zipper, we can "unwrap" the data structure by calling `z/root` on it. Parsing the sample input into a filesystem tree looks like this:

```clojure
(->> sample-input
     str/split-lines
     (reduce parse nil)
     z/root)
```

So what does `parse` do?

Given a loc and line of input, it matches one of the six possibilities and returns the resulting loc. The possibilities are:

1. `$ cd /` creates a new zipper with it's starting node as the directory `/`. I probably could (should?) have done this outside of `parse` since it only happens exactly once, but I liked the uniformity of having all the possible commands parsed and appearing here in this one place.
2. `$ ls` is a no-op and returns the loc unchanged.
3. `dir (.+)` matches the listing of a directory and captures the directory name. `condp` has a neat little feature where you can have a clause take the form `test-expr :>> result-fn` instead of the typical `test-expr result-expr`. With this form, `result-fn` is meant to be a function that takes the result of the predicate: in this case, that means that `(partial insert-dir loc)` will be called with the result of `(re-find #"dir (.+)" input)` as its input. For example, if the input were `"dir a"`, then we would effectively be calling `(insert-dir loc ["dir a" "a"])` and returning the result.
4. `(\d+) (.+)` is similar to the above but matches the listing of a file and captures the file size and name. For example, if the input were `"14848514 b.txt"`, the we would effectively be calling `(insert-file loc ["14848514 b.txt" "14848514" "b.txt"])` and returning the result.
5. `cd ..` returns the result of navigating "up" from the current loc. This was the part that really motivated me to try out zippers: it's just so clean and maps one-to-one with the problem statement, no extra gymnastics on my part to keep track of things.
6. `cd (.+)` takes care of navigation down the tree into subdirectories. It used the same `condp` feature as the directory and file cases. For example, if the input were `"cd a"`, the we would effectively be calling `(nav-to-dir loc ["cd a" "a"])` and returning the result.

Next, let's take a closer look at the functions that manipulate our tree. We'll start with creating the zipper:

```clojure
(defn fs-zipper [root]
  (z/zipper dir? :children (fn [n coll] (assoc n :children coll)) root))
```

In order to create a zipper, we need to provide a few things:

1. A predicate that determines whether a node is a branch (in our case, filesystem directories are branches)
2. A function to retrieve the child nodes of a branch
3. A function to set the child nodes of a given node to the given collection
4. The root node of the zipper (the root directory in our case)

Inserting directories and files is fairly straightforward:

```clojure
(defn insert-dir [loc [_ name]]
  (z/append-child loc (dir name)))

(defn insert-file [loc [_ size name]]
  (z/append-child loc (file name size)))
```

In both cases, given a loc and the regex match from `parse`, we destructure the match to get the relevant bits and append the appropriate node. Navigating to a given directory is a little more involved, but not by much:

```clojure
(defn dir-loc [name loc]
  (let [file (z/node loc)]
    (when (and (dir? file) (= (:name file) name))
      loc)))

(defn nav-to-dir [loc [_ name]]
  (some (partial dir-loc name) (->> loc z/down (iterate z/right))))
```

`dir-loc` returns the given loc if it represents a directory with the given name, or `nil` otherwise. This allows the function to play nice with `some` in `nav-to-dir`, which returns the first truthy value. So what exactly is `nav-to-dir` doing? First, it creates a lazy seq of all the children of the node at loc: `z/down` navigates to the first child and `(iterate z/right)` iterates through them all. Then it calls `some` on that seq, looking for the first node that is a directory with the given name.

## Calculating Directory Sizes

Now that we have the filesystem represented, the next step is to determine the sizes of the directories. In a language like Java, this is fairly straight forward: perform a post-order depth-first traversal of the tree and modify each directory node to have a file size being the sum of its child nodes. In Clojure, I wasn't exactly sure how to approach this, because I'd need to produce a new tree (or at least swap in new nodes) containing directories with sizes. As with the first part of the puzzle, I smelled recursion that I wasn't particularly excited to deal with. Thanks to `clojure.walk`, I didn't have to! There's a `postwalk` function in that namespace that just *figures out* how to traverse the data structure you give it, provided it's tree-shaped in some way. You also give it a function to transform a node, and it will run this function on each node, swapping the result in to the data structure. Here's what that looks like:

```clojure
(defn total-size [file]
  (if-let [children (:children file)]
    (assoc file :size (apply + (map :size children)))
    file))
    
(defn calculate-dir-sizes [input]
  (->> input
       str/split-lines
       (reduce parse nil)
       z/root
       (walk/postwalk total-size)))
```

Note that `calculate-dir-sizes` is the same code shown earlier that did the parsing, but it finishes up with a `postwalk`. As mentioned above, `postwalk` takes a function that transforms a node: `total-size`. This function does pretty much what you would expect: if there are children (i.e. this node is a directory), `assoc` in the sum of the sizes of all the children; otherwise, leave the node unchanged.

## Filter and Sum the Directories

There's one interesting bit left before we coast across the finish line: how do we get Clojure to traverse the filesystem structure we've built? It turns out there's a function for that too! Since our structure is representing a tree, we can adapt it to Clojure's sequence abstraction by calling `tree-seq` on it. `tree-seq` works similarly to constructing a zipper where we have to tell it how to identify branch nodes and how to get child nodes from a branch node. In our case that's:

```clojure
(tree-seq dir? :children filesystem)
```

With a tree-seq in hand, we're good to use some familiar functions to perform the remaining calculations:

```clojure
(->> sample-input
     calculate-dir-sizes
     (tree-seq dir? :children)
     (filter (every-pred dir? #(<= (:size %) 100000)))
     (map :size)
     (apply +))
;; => 95437
```

## Final Remarks

I'm pretty happy with how this one turned out. It took a while to figure out how to use these new namespaces, but I'm sure the amount of frustration they saved over doing the recursion myself was well worth it. In general, this one really hammered home something I remember from the Functional Design in Clojure podcast[^2] (paraphrasing): your job is to teach Clojure your domain language by writing predicates, reducing functions, etc. and then leveraging the excellent core library to do the actual work. If you look back at the code I had to write, most of it is exactly that: I define what directories and files are, how to make a filesystem zipper, what it means to insert a node in the filesystem, and how to transform a directory to include a size. The rest is pretty much just gluing existing Clojure functions together (and parsing strings, but that's also pretty domain-specific).

It also reenforced how important it is to get your data in the right shape for the problem you're trying to solve. Once the filesystem was built up and populated with file sizes for directories, the rest of the work was pretty simple. It's classic divide-and-conquer: take the problem and break it into two. The first solves the problem assuming the data is in an easy-to-use shape; the second parses the input into that easy-to-use shape.

I had a lot of fun on this one, and I'll be keeping my eyes open for other treasures in the standard library!

[^1]: You can find all the code [here](https://github.com/jbullers/advent_of_code/blob/main/src/advent2022/day07.clj), including my solution for part 2 of the puzzle. I didn't bother covering it above because it was pretty trivial by comparison and followed easily from part 1.
[^2]: <https://clojuredesign.club/>
